dnl Process this file with autoconf to produce a configure script.

AC_INIT(GNOME Inform 7, 5T18, http://www.inform-fiction.org)
AM_INIT_AUTOMAKE(gnome-inform7, 5T18)
AM_MAINTAINER_MODE
AM_CONFIG_HEADER(config.h)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AM_PROG_CC_C_O
AC_HEADER_STDC
AC_PROG_RANLIB
AM_GCONF_SOURCE_2
AC_PATH_PROG(GCONFTOOL, gconftool-2)

pkg_modules="gtk+-2.0 >= 2.8.0 libgnomeui-2.0 gtksourceview-2.0 gtkspell-2.0" 
PKG_CHECK_MODULES(GNOMEINFORM7, [$pkg_modules])

dnl Check for the correct UUID library header location
PKG_CHECK_MODULES(UUID, ossp-uuid,
	[AC_CHECK_HEADER(ossp/uuid.h,
		AC_DEFINE([OSSP_UUID], [], [Define if the OSSP UUID header is <ossp/uuid.h>]),
		[])],
	[PKG_CHECK_MODULES(UUID, uuid,
		[AC_DEFINE([E2FS_UUID], [], [Define if uuid header is from e2fs])],
		[AC_MSG_ERROR([Either ossp-uuid or e2fsprogs uuid required.])])]
	)

dnl Check for GtkHTML 3.8 or 3.14
PKG_CHECK_MODULES(GTKHTML, libgtkhtml-3.14,
	[AC_DEFINE([GTK_HTML_314], [], [Define if using GtkHTML 3.14 API])],
	[
		AC_MSG_NOTICE([GtkHTML 3.14 not found])
		PKG_CHECK_MODULES(GTKHTML, libgtkhtml-3.8,
			[AC_DEFINE([GTK_HTML_38], [], [Define if using GtkHTML 3.8 API])],
    		[AC_MSG_ERROR([GtkHTML 3.8 or higher is required.])])
	])

PKG_CHECK_MODULES(GTKTERP, [gtk+-2.0 >= 2.8.0 freetype2 sdl libpng dbus-glib-1])
GTKTERP_LIBS="$GTKTERP_LIBS -lSDL_mixer -ljpeg"
AC_SUBST(GTKTERP_CFLAGS)
AC_SUBST(GTKTERP_LIBS)

GNOMEINFORM7_CFLAGS="$GNOMEINFORM7_CFLAGS $UUID_CFLAGS $GTKHTML_CFLAGS"
GNOMEINFORM7_LIBS="$GNOMEINFORM7_LIBS $UUID_LIBS $GTKHTML_LIBS"
AC_SUBST(GNOMEINFORM7_CFLAGS)
AC_SUBST(GNOMEINFORM7_LIBS)

dnl Add user c-flags
CFLAGS=$CFLAGS

dnl ***************************************************************************
dnl Internatinalization
dnl ***************************************************************************
GETTEXT_PACKAGE=gnome-inform7
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [Gettext package.])
dnl Add the languages which your application supports here.
ALL_LINGUAS=""
AM_GLIB_GNU_GETTEXT
IT_PROG_INTLTOOL([0.35.0])

AC_OUTPUT([
Makefile
src/Makefile
src/gtkterp/Makefile
src/gtkterp/frotz/Makefile
src/gtkterp/glulxe/Makefile
src/gtkterp/garglk/Makefile
po/Makefile.in
data/Makefile
data/Compilers/Makefile
data/Documentation/Makefile
data/Documentation/doc_images/Makefile
data/Documentation/gnome/Makefile
data/Documentation/licenses/Makefile
data/Documentation/Sections/Makefile
data/Inform7/Makefile
data/Inform7/Extensions/Makefile
data/Inform7/Extensions/DavidFisher/Makefile
data/Inform7/Extensions/EmilyShort/Makefile
data/Inform7/Extensions/GrahamNelson/Makefile
data/Inform7/Extensions/Reserved/Makefile
data/Inform7/Extensions/Reserved/Templates/Makefile
data/Library/Makefile
data/Library/Natural/Makefile
data/map_icons/Makefile
data/scene_icons/Makefile
data/styles/Makefile
])
